buildscript {
    ext {
        springBootVersion = '1.5.14.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'

group = 'us.cuatoi.s34j'
version = '0.7.2'
sourceCompatibility = 1.8

repositories {
    mavenCentral()
}


dependencies {
    compile('com.fasterxml.jackson.core:jackson-databind:2.8.8')
    compile('com.google.guava:guava:23.3-jre')
    compile('com.google.cloud:google-cloud-pubsub:1.31.0')
    compileOnly('org.springframework.boot:spring-boot-starter')
    testCompile('org.springframework.boot:spring-boot-starter-test')
    testCompile('org.springframework.boot:spring-boot-starter-web')
}

bootRepackage {
    enabled = false
}

/**
 * Find bugs plugins
 */

apply plugin: 'findbugs'
tasks.withType(FindBugs) {
    reports {
        xml.enabled false
        html.enabled true
    }
}

/**
 * Maven deployment configuration
 */
task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'S34J Google Pub Sub'
                packaging 'jar'
                description 'A spring boot google pub sub client.'
                url 'https://s34j.cuatoi.us'

                scm {
                    url 'https://github.com/dungviettran89/s34j/'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'dungviettran89'
                        name 'Tran Viet Dung'
                        email 'dungviettran89@gmail.com'
                    }
                }
            }
        }
    }
}

