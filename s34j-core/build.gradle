buildscript {
    repositories {
        mavenCentral()
    }
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

group = 'us.cuatoi.s34j'
version = '0.7.3'
sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile('org.apache.commons:commons-lang3:3.6')
    compile('org.slf4j:slf4j-api:1.7.25')
    compile('commons-io:commons-io:2.6')
    compile('com.google.guava:guava:23.3-jre')
    compile('com.google.code.gson:gson:2.8.2')
    compile('org.modelmapper:modelmapper:1.1.2')
    compile('org.apache.httpcomponents:httpclient:4.5.4')
    compile('com.google.http-client:google-http-client-xml:1.23.0')
    compile('joda-time:joda-time:2.9.9')

    compileOnly('javax.servlet:javax.servlet-api:3.1.0')
    compileOnly('org.springframework:spring-core:4.3.13.+')

    testCompile('junit:junit:4.12')
    testCompile('org.slf4j:slf4j-simple:1.7.25')
}

/*
 *
 *  * Copyright (C) 2018 dungviettran89@gmail.com
 *  *
 *  * Licensed under the Apache License, Version 2.0 (the "License"); you may not use this file except
 *  * in compliance with the License. You may obtain a copy of the License at
 *  *
 *  * http://www.apache.org/licenses/LICENSE-2.0
 *  *
 *  * Unless required by applicable law or agreed to in writing, software distributed under the License
 *  * is distributed on an "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express
 *  * or implied. See the License for the specific language governing permissions and limitations under
 *  * the License.
 *  *
 *
 */

/**
 * Maven deployment configuration
 */
task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'S3 For Java'
                packaging 'jar'
                description 'A simple implementation of Amazon S3 written in Java using plain servlet api.'
                url 'https://s34j.cuatoi.us'

                scm {
                    url 'https://github.com/dungviettran89/s34j/'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'dungviettran89'
                        name 'Tran Viet Dung'
                        email 'dungviettran89@gmail.com'
                    }
                }
            }
        }
    }
}

